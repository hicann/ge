function(do_add_codegen_e2e_st_test)
    set(one_value_arg
        WORKDIR # Workdir
        )
    set(mul_value_arg
        TILING # Tiling codegen library source file
        CODEGEN # Codegen library source file
        KERNEL_SRC # Kernel source file that codegen will generate
        TEST_SRC # Test case source file
        )

    set(TEST_NAME ${ARGV0})
    cmake_parse_arguments(PARSE_ARGV 1 ARG "" "${one_value_arg}" "${mul_value_arg}")

    message(STATUS "ARG_WORKDIR=${ARG_WORKDIR}")

    foreach(file ${ARG_KERNEL_SRC})
        list(APPEND KERNEL_SRC "${ARG_WORKDIR}/${file}")
    endforeach()

    set(ATT_SO_NAME ${TEST_NAME}_gen_tiling) #拼接so的名字
    set(E2E_ST1_GENERATOR_EXE_NAME ${TEST_NAME}_codegen)
    set(E2E_ST2_EXE_KERNEL_EXE_NAME ${TEST_NAME}_e2e)

    add_library(${ATT_SO_NAME} SHARED ${ARG_TILING})
    target_include_directories(${ATT_SO_NAME} PRIVATE ${CMAKE_CURRENT_SOURCE_DIR}/../)
    target_link_libraries(${ATT_SO_NAME} codegen)

    add_executable(${E2E_ST1_GENERATOR_EXE_NAME} ${ARG_CODEGEN})
    target_link_libraries(${E2E_ST1_GENERATOR_EXE_NAME}
                          ${ATT_SO_NAME}
                          codegen
                          e2e_com
                          ascgen_common
                          aihac_symbolizer
                          error_manager
                          metadef
                          json
                          GTest::gtest
                          GTest::gtest_main)


    list (JOIN ARG_KERNEL_SRC ":" KERNEL_SRC_LIST)
    message(STATUS "KERNEL_SRC_LIST = ${KERNEL_SRC_LIST}")

    target_compile_definitions(${E2E_ST1_GENERATOR_EXE_NAME} PRIVATE
       ATT_SO_NAME=\"./lib${ATT_SO_NAME}.so\"
       KERNEL_SRC_LIST=\"${KERNEL_SRC_LIST}\"
    )

    # 一个完整ST用例的两个ST用例源2文件放在一个目录下
    execute_process(COMMAND touch ${ARG_KERNEL_SRC}   # 创建第一类用例生成的空文件
                    WORKING_DIRECTORY ${ARG_WORKDIR})

    add_custom_target(call_${E2E_ST1_GENERATOR_EXE_NAME} ALL
                      COMMAND ${E2E_ST1_GENERATOR_EXE_NAME}
                      DEPENDS ${E2E_ST1_GENERATOR_EXE_NAME}
                      WORKING_DIRECTORY ${ARG_WORKDIR})

    add_executable(${E2E_ST2_EXE_KERNEL_EXE_NAME} ${KERNEL_SRC} ${ARG_TEST_SRC})
    add_dependencies(${E2E_ST2_EXE_KERNEL_EXE_NAME} call_${E2E_ST1_GENERATOR_EXE_NAME})
    target_include_directories(${E2E_ST2_EXE_KERNEL_EXE_NAME} PRIVATE ${ARG_WORKDIR})
    target_link_libraries(${E2E_ST2_EXE_KERNEL_EXE_NAME} tikicpulib_ascend910B1 GTest::gtest GTest::gtest_main)
    target_compile_options(${E2E_ST2_EXE_KERNEL_EXE_NAME} PRIVATE -DAUTO_FUSE_DEVICE=1)
    #gtest_discover_tests(${E2E_ST2_EXE_KERNEL_EXE_NAME})
endfunction()

macro(add_codegen_e2e_st_test)
    do_add_codegen_e2e_st_test(${ARGV}  WORKDIR ${CMAKE_CURRENT_BINARY_DIR})
endmacro()
